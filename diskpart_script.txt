rem DISKPART script for single Windows partition
rem OS: Windows 10 (any version & edition)
rem ---------------------------------------------------
rem Select Disk, wipe it empty, convert to GPT
rem 
select disk 0
clean
convert gpt
rem
rem ---------------------------------------------------
rem Create & format 100 MB EFI System partition 
rem 
create partition efi size=100
format quick fs=fat32 label="System"
rem
rem ---------------------------------------------------
rem Create 16 MB MSR partition (will not be formatted)
rem 
create partition msr size=16
rem
rem ---------------------------------------------------
rem Create OS partition using all available space, 
rem shrink it with 450 MB to leave space at end of HDD
rem for WinRE partition
rem 
create partition primary 
shrink minimum=450
rem
rem ---------------------------------------------------
rem Format OS partition, label it, assign drive letter
rem W. Windows Setup will change this drive letter to C
rem when installed. It's important now to use a not
rem reserved letter, therefore we use a letter from
rem end of alphabet
rem 
format quick fs=ntfs label="Windows 10"
assign letter="W"
rem
rem ---------------------------------------------------
rem Create & format WinRE recovery partition at the 
rem end of the disk. Not defining the size, it will use
rem all available space, 450 MB that we shrunk OS
rem partition with. Notice that ID and GPT attributes
rem must be set exactly as shown! 
rem 
create partition primary
format quick fs=ntfs label="WinRE"
set id="de94bba4-06d1-4d40-a16a-bfd50179d6ac"
gpt attributes=0x8000000000000001
rem
rem ---------------------------------------------------
rem Exit Diskpart 
rem 
exit